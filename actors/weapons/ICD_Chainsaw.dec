ACTOR ICD_Chainsaw : BaseWeapon replaces Chainsaw
{
	+WEAPON.NOALERT
	Inventory.PickupMessage "$GotChainsaw"
	Obituary "%o got too close to %k's chainsaw!"
	Tag "Chainsaw"
	Scale 0.75
	Height 16
	Radius 20
	States
	{
		Select:
			SAWG I 1 A_Raise
			Wait
		Deselect:
			SAWG C 1 A_Lower
			Wait
		Ready:
			Goto Ready.Rev
		Ready.On:
			SAWG C 4 A_WeaponReady
			TNT1 A 0 A_PlaySound("weapons/chainsaw/sawidle2", CHAN_WEAPON)
			TNT1 A 0 A_FireCustomMissile("ICD_ChainsawSmokeSpawner",0,0,6,-8)
			SAWG D 3 A_WeaponReady
			Loop
		Ready.Rev:
			SAWG E 4
			SAWG F 2
			SAWG G 2
			TNT1 A 0 A_FireCustomMissile("ICD_ChainsawSmokeSpawner",0,0,6,-8)
			TNT1 A 0 A_Jump(107,4)// Jump Below
			SAWG H 2 A_PlaySound("weapons/chainsaw/sawidle2", CHAN_WEAPON)
			SAWG G 4
			SAWG F 6
			Goto Ready.Rev+2
			TNT1 A 0 A_GiveInventory("ICD_ChainsawMechanic", 1)
			SAWG H 2 A_PlaySound("weapons/chainsaw/sawup2", CHAN_WEAPON)
			SAWG G 3
			SAWG F 2
			SAWG D 4
			Goto Ready.On
		Fire:
			TNT1 A 0 A_AlertMonsters
			SAWG A 3 A_Saw("weapons/chainsaw/sawful2", "weapons/chainsaw/hit2", 5, "ICD_BayonetSpark", 0, 64, 12, 6)
			TNT1 A 0 A_FireCustomMissile("ICD_ChainsawSmokeSpawner",0,0,6,-8)
			SAWG B 3 A_Saw("weapons/chainsaw/sawful2", "weapons/chainsaw/hit2", 5, "ICD_BayonetSpark", 0, 64, 12, 6)
			TNT1 A 0 A_FireCustomMissile("ICD_ChainsawSmokeSpawner",0,0,6,-8)
			Goto Ready.On
		Spawn:
			CSAW A -1
			Stop
	}
}

ACTOR ICD_ChainsawMechanic : Inventory
{
	Inventory.MaxAmount 1
	+INVENTORY.IGNORESKILL
	-INVBAR
}